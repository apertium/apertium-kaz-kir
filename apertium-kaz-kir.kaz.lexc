!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!   M O R P H O L O G I C A L · T R A N S D U C E R · F O R · K A Z A K H   !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! See http://wiki.apertium.org/wiki/Turkic_lexicon

Multichar_Symbols
! Followed http://wiki.apertium.org/wiki/Turkic_languages

! Part of speech categories = First-level tags
%<n%>         ! Noun                       ! Зат есім
%<np%>        ! Proper noun                ! Жалқы есім
%<adj%>       ! Adjective                  ! Сын есім
%<num%>       ! Numeral                    ! Сан есім
%<prn%>       ! Pronoun                    ! Есімдік
%<det%>       ! Determiner                 ! Детерминатив
%<v%>         ! Verb                       ! Етістік
%<vaux%>      ! Auxilary verb              ! Көмекші етістік
%<adv%>       ! Adverb                     ! Үстеу
%<post%>      ! Postposition               ! Септеулік шылау
%<postadv%>   ! Postadverb                 ! "Постүстеу" (*1)
%<cnjcoo%>    ! Co-ordinating conjunction  ! Cалаластырғыш жалғаулық
%<cnjsub%>    ! Sub-ordinating conjunction ! Cабақтастырғыш жалғаулық
%<cnjadv%>    ! Adverbial conjunction      ! "Үстеу-жалғаулық"
%<ij%>        ! Interjection               ! Одағай
%<abbr%>      ! Abbreviation               ! Қысқарған сөз
%<cop%>       ! Copula                     ! Копула
%<part%>      ! Particle                   ! Демеулік
%<ideo%>      ! Ideophone                  ! Еліктеу сөз

! *1. Анықтаған сөздерінің артынан келетін үстеулер ("демеуліктер") !# ғана, -ақ

! Proper noun types
%<top%>       ! Toponym
%<ant%>       ! Anthroponym  ! Кісі есімі
%<cog%>       ! Cognomen     ! Фамилия
%<pat%>       ! Patronym     ! Әке текті есім
%<org%>       ! Organization
%<al%>        ! Other        ! Басқалар

!! gender of anthroponyms and cognoms
%<m%>         ! Masculine
%<f%>         ! Feminine
%<mf%>        ! Masculine/feminine !# basically cognoms without -ов/-ова,
                                   ! -ин/-ина endings

! "Syntactic" tags. Attributive use of non-adjectives etc.
%<attr%>      ! Attributive
%<subst%>     ! Substantive
%<advl%>      ! Adverbial

! Number
%<sg%>        ! Singular
%<pl%>        ! Plural

! Possessives
%<px1sg%>     ! First person singular
%<px2sg%>     ! Second person singular
%<px3sp%>     ! Third person singular/plural
%<px1pl%>     ! First person plural
%<px2pl%>     ! Second person plural
%<px%>        ! General possessive          !# -нікі

! Cases
%<nom%>       ! Nominative
%<gen%>       ! Genitive
%<dat%>       ! Dative
%<acc%>       ! Accusative
%<abl%>       ! Ablative
%<loc%>       ! Locative
%<ins%>       ! Instrumental

!! some additional ~cases
%<sim%>       ! Similative (<apprx> in kymorph)
              !# DAй
%<abe%>       ! Abessive=Privative ! Лишительный
              !# -сыз/-сез (not used after posessives and cases)
%<reas%>      ! Reason
!обладательный !# LI (-лы/-ле) (not used after posessives and cases)

! Levels of comparison of adjectives
%<comp%>      ! Comparative
%<sup%>       ! Superlative

! Pronoun types
%<pers%>      ! Personal
%<recip%>     ! Reciprocal
! Pronoun&Determiner types
%<dem%>       ! Demonstrative
%<ind%>       ! Indefinite
%<itg%>       ! Interrogative
%<qnt%>       ! Quantifier
%<neg%>       ! Negative       !# еш нерсе
              ! (NOTE: also used to denote negation in verbs, i.e for м{A})
%<ref%>       ! Reflexive

! Numeral types
%<ord%>       ! Ordinal
%<coll%>      ! Collective
%<dist%>      ! Distributive

! Verbal features

!! Mood
%<imp%>       ! Imperative
%<opt%>       ! Optative/jussive

!! Derivation
%<caus%>      ! Causative
%<pass%>      ! Passive
%<coop%>      ! Cooperative

!! Tense / finite forms
%<pres%>      ! for "жатыр", "тұр", "отыр" and "жүр"
%<aor%>       ! -{E}                    ! non-past (afaik /IS/)
%<past%>      ! -{G}{A}н                ! past
%<ifi%>       ! -{D}{I}                 ! eyewitness past
%<ifi_evid%>  ! -{I}пт{I}               ! "reported past" (afaik /IS/)
%<fut%>       ! -{I}р                   ! future
%<fut_plan%>  ! -{M}{A}{K}              ! future plan

%<evid%>      ! evidential
              ! Was used for -ган екен and -ыпты. The former is not handled here
              ! in lexc anymore, the latter was renamed from <ifi><evid> to
              ! <ifi_evid>

!! Non-Finite verb forms

!!! Participles
%<prc_perf%>  ! -{I}п                   ! perfect participle
              !# "Бірақ мысығы үйде, _ұйықтап_ жатыр.";
%<prc_impf%>  ! -{E}                    ! imperfect participle
              !# "...олар далада _ойнай_ алмады...";
%<prc_vol%>   ! -{G}{I}                 ! volition participle
              !# барғым келмейді;
%<prc_cond%>  ! -с{A}                   ! conditional participle
              !# ...жесең болады...;
%<prc_fplan%> ! -{M}{A}{K},             ! future plan participle
              ! -{M}{A}{K}ш{I} Dir/LR
              !# "Шал қазға бармақшы болады.";
%<prc_plan%>  ! -{G}{A}л{I}             ! plan participle
              !# мен сөйлескелі келдім;
!%<prc_irre%> ! -{E}{T}{I}н             ! irrealis participle
              ! (This form is analyzed as <grp_impf> and <ger_impf>)
              ! (FIXME this might be wrong)

!!! Verbal adverbs - Глагольные наречия
%<gna_perf%>  ! -{I}п
              !# "...ул вакытта инде кояш _баеп_, йолдызлар күренә башлаган
              !# иде..." (Ф.Хөсни);
%<gna_cond%>  ! -с{A}
              !# ...қайда екенін _білсе_, маған бұл туралы айтыр еді...;
%<gna_until%> ! -{G}{A}нш{A}
              !# "Мен кеткенше ол жауап бермейді.";
%<gna_after%> ! -{G}{A}л{I}             (ambiguous with prc_plan)
              !# "Сабақ _біткелі_ екі сағат өтті."; Ол кеткелі біз жұмыс
              !# істемейдік;

!!! Verbal adjectives - Глагольные причастия
%<gpr_past%>  ! -{G}{A}н               ! past verbal adjective
              !# келген адам; оқылмаған кітап;
%<gpr_impf%>  ! -{E}{T}{I}н            ! imperfect verbal adjective
              !# басқаратын = руководящий
%<gpr_pot%>   ! -{U}ш{I}               ! potential verbal adjective
              !# "...өзінің қадір-қасиетін арттыруға _тырысушы_ саясаткер...";
%<gpr_ppot%>  ! -{A}рл{I}{K}
              !# сүйсінерлік ерлік;
%<gpr_fut%>   ! -{I}р                  ! future verbal adjective
              !# ""Барар жерің Балкан тау, ол да біздің көрген тау";

!!! Gerunds (verbal nouns)
%<ger%>       ! -{U}
%<ger_past%>  ! -{G}{A}н
%<ger_perf%>  ! -{G}{A}нл{I}{K}   (stresses the fact that something happened)
%<ger_ppot%>  ! -{A}рл{I}{K}      (~the ability to do the denoted action)
              !# "_Сүйсінерлігі_ сол, өз сеніміне берік те адал халқымыз дінін
              !# сатпады, өзгенің тәтті де сылдыр сөзіне ермеді...";
%<ger_abs%>   ! -уш{I}л{I}{K}          FIXME CHECK
%<ger_fut%>   ! -{A}р
              !# "Мен айтарымды айттым";
%<ger_impf%>  ! -{E}{T}{I}н
              !# "Ол бүгін кешкілік болатынын айтты";
! FIXME cover -{E}{T}{I}нд{I}{K} form

!! Transitivity
%<tv%>        ! Transitive
%<iv%>        ! Intransitive
%<TD%>        ! Temporary tag

! Person
%<p1%>        ! First person
%<p2%>        ! Second person
%<p3%>        ! Third person
%<frm%>       ! Formality

! Modal particles
%<qst%>       ! Modal question particle
%<mod_foc%>   ! "Particles of politeness and ardent desire"
              ! (as in [N. Poppe. Tatar Manual]) or
              ! "what about ..." question particles" :)
              ! (as in kir.lexc)
%<mod_ass%>   ! Assertive modal particle
%<mod%>       ! Modal verbs (шығар, сияқты etc)

! Punctuation
%<sent%>      ! Sentence marker
%<guio%>      ! Hyphen
%<cm%>        ! Comma
%<apos%>      ! Apostrophe
%<rquot%>     ! Quote marker (right hand side)
%<lquot%>     ! Quote marker (left hand side)
%<rpar%>      ! Parenthetical marker (right hand side)
%<lpar%>      ! Parenthetical marker (left hand side)

%{L%}         ! Archiphoneme 'l': realised as л, д, т
%{N%}         ! Archiphoneme 'n': realised as н, д, т
%{M%}         ! Archiphoneme 'm': realised as м, б, п
%{G%}         ! Archiphoneme 'g': realised as к, қ, г, ғ
%{G%}         ! Archiphoneme 'k': realised as к, қ  >>>FIXME ???<<<
%{D%}         ! Archiphoneme 'd': realised as д, т
%{A%}         ! Archiphoneme 'a': realsied as е, а
%{I%}         ! Archiphoneme 'i': realised as і, ы
%{S%}         ! Realised as 'с' or ''; only used in 3rd person morphology
%{K%}         ! Realised as 'к' or 'қ'
%{n%}         ! Realised as 'н' or '';
              ! only used in 3rd person morphology and -NIKI
%{l%}         ! Realised as 'л' or 'н'; only used in passive -{I}{l}
%{y%}         ! Realised as '' or '{I}'; only used in epenthesis for nouns
%{o%}         ! Realised as ''; triggers dialectal interpretation of Iп
%{E%}         ! Realised as 'а', 'е', 'й' (={A}/й)
%{д%}         ! Realised as д before vowels, 0 otherwise
%{т%}         ! Realised as т before vowels, 0 otherwise


%{а%}         ! Archiphoneme for back vowel numerals/abbreviation
%{э%}         ! Archiphoneme for front vowel numerals/abbreviation

%{й%}         !
%{л%}         !
%{н%}         !
%{з%}         !
%{т%}         !
%{с%}         !

%{☭%}         ! Used to tell the phonology about a Russian word

%{❗%}         ! Obstacles on the road!!!
%{ъ%}         ! Force back harmony

%>            ! Morpheme boundary
%             ! Space
%-            !
%,            !
%.            !
%%            !

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

ModalParticles ;
Copula ;
Conjunctions ;
Postpositions ;
Postadverbs ;
Pronouns ;
Determiners ;
Numerals ;
Nouns ;
ProperNouns ;
Adjectives ;
Adverbs;
Verbs ;
Interjections ;
Ideophones ;
Abbreviations ;
Punctuation ;
Digits ; ! Use/Circ
!Guesser ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!
!!!!                            N U M E R A L S 
!!!!
!!!! WARNING: This section should not be modified without consultation. 
!!!!          Test cases are in dev/numerals.
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON CASE-NONOM

! This continuation lexicon is a copy of the normal case lexicon,
! only it does not include nominative 
!   1<num><nom> = 1, 1<num><acc> = 1-{N}{I} 

%<gen%>:%>%{N%}%{I%}ң CLIT ;
%<dat%>:%>%{G%}%{A%} CLIT ;
%<acc%>:%>%{N%}%{I%} CLIT ;
%<abl%>:%>%{D%}%{A%}н # ;
%<loc%>:%>%{D%}%{A%} # ;
%<ins%>:%>%{M%}ен CLIT ;

LEXICON CASE-ETC-NONOM

CASE-NONOM ;
GENPOSS-ETC ;

LEXICON N-INFL-COMMON-NONOM

POSSESSION ;
CASE-ETC-NONOM ;

LEXICON SUBST-NONOM

N-INFL-COMMON-NONOM ;
%<pl%>:%>%{L%}%{A%}р N-INFL-COMMON-NONOM ;

LEXICON SQRD 
! This lexicon includes common ways of writing squared, cubed etc.

²%<num%>:2 # ;  ! Dir/LR
³%<num%>:3 # ;  ! Dir/LR
²%<num%>:² # ; 
³%<num%>:³ # ; 
%<num%>: # ; 

LEXICON NUM-DIGIT

%<num%>: # ;
%<num%>%<subst%>%<nom%>: # ;
%<num%>%<subst%>:%- SUBST-NONOM ;
%<num%>%<subst%>: SUBST-NONOM ; ! Dir/LR

%<num%>%<ord%>:%-%{I%}нш%{I%} # ;
%<num%>%<ord%>%<subst%>%<nom%>:%-%{I%}нш%{I%} # ;
%<num%>%<ord%>%<subst%>:%-%{I%}нш%{I%} SUBST-NONOM ;
%<num%>%<ord%>:%-ш%{I%} # ; ! Dir/LR
%<num%>%<ord%>%<subst%>%<nom%>:%-ш%{I%} # ; ! Dir/LR
%<num%>%<ord%>%<subst%>:%-ш%{I%} SUBST-NONOM ; ! Dir/LR

%<num%>%<coll%>%<advl%>:%-%{A%}у # ;
%<num%>%<coll%>%<nom%>:%-%{A%}у # ;
%<num%>%<coll%>:%-%{A%}у SUBST-NONOM ;

%<num%>%<percent%>:%% # ;
%<num%>%<percent%>%<subst%>%<nom%>:%% # ;

%<num%>%<percent%>%<subst%>:%%%{а%}%{з%}%- SUBST-NONOM ;
%<num%>%<percent%>%<subst%>:%%%{а%}%{з%} SUBST-NONOM ; ! Dir/LR
%<num%>%<percent%>%<subst%>:%%%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR
%<num%>%<percent%>%<subst%>:%%%{э%}%{с%} SUBST-NONOM ; ! Dir/LR

°С%<num%>:°С # ;
°С%<num%>%<subst%>%<nom%>:°С # ;
°С%<num%>%<subst%>:°С%{а%}%{с%}%- SUBST-NONOM ;
°С%<num%>%<subst%>:°С%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR
°С%<num%>:ºС # ; ! Dir/LR
°С%<num%>%<subst%>%<nom%>:ºС # ; ! Dir/LR
°С%<num%>%<subst%>:ºС%{а%}%{с%}%- SUBST-NONOM ; ! Dir/LR
°С%<num%>%<subst%>:ºС%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR
°С%<num%>:С # ; ! Dir/LR
°С%<num%>%<subst%>%<nom%>:С # ; ! Dir/LR
°С%<num%>%<subst%>:С%{а%}%{с%}%- SUBST-NONOM ; ! Dir/LR
°С%<num%>%<subst%>:С%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR

С%<num%>:° # ; ! Dir/LR
С%<num%>%<subst%>%<nom%>:° # ; ! Dir/LR
С%<num%>%<subst%>:°%{а%}%{с%}%- SUBST-NONOM ; ! Dir/LR
С%<num%>%<subst%>:°%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR

! Measurement units

км:км SQRD ;
мм:мм SQRD ;
см:см SQRD ;
м:м SQRD ;

LEXICON LASTDIGIT

1:1%{э%}%{й%}           NUM-DIGIT ; ! "бір"
2:2%{э%}                NUM-DIGIT ; ! "екі"
3:3%{э%}%{с%}           NUM-DIGIT ; ! "үш"
4:4%{э%}%{с%}           NUM-DIGIT ; ! "төрт"
5:5%{э%}%{с%}           NUM-DIGIT ; ! "бес"
6:6%{а%}                NUM-DIGIT ; ! "алты"
7:7%{э%}                NUM-DIGIT ; ! "жеті"
8:8%{э%}%{з%}           NUM-DIGIT ; ! "сегіз"
9:9%{а%}%{з%}           NUM-DIGIT ; ! "тоғыз"

LEXICON LASTDIGIT-REST

1%0:1%0%{а%}%{н%}       NUM-DIGIT ; ! "он"
2%0:2%0%{а%}            NUM-DIGIT ; ! "қырық"
3%0:3%0%{а%}%{з%}       NUM-DIGIT ; ! "отыз"
4%0:4%0%{а%}%{с%}       NUM-DIGIT ; ! "қырық"
5%0:5%0%{э%}%{й%}       NUM-DIGIT ; ! "елу"
6%0:6%0%{а%}%{с%}       NUM-DIGIT ; ! "алпыс"
7%0:7%0%{э%}%{с%}       NUM-DIGIT ; ! "жетпіс"
8%0:8%0%{э%}%{н%}       NUM-DIGIT ; ! "сексен"
9%0:9%0%{а%}%{н%}       NUM-DIGIT ; ! "тоқсан"

LEXICON POWERS 
%0%0:%0%0%{э%}%{з%}     NUM-DIGIT ; ! "жүз"
%0%0%0:%0%0%0%{а%}%{н%} NUM-DIGIT ; ! "мың"

LEXICON LOOP

%,:%, DIGITLEX ; 
%.:%. DIGITLEX ; 
%,:%, LASTDIGIT ; 
%.:%. LASTDIGIT ; 
%,:%, LASTDIGIT-REST ; 
%.:%. LASTDIGIT-REST ; 
%,:%, POWERS ; 
%.:%. POWERS ; 
      DIGITLEX ; 
      LASTDIGIT ;
      LASTDIGIT-REST ;
      POWERS ; 

LEXICON DIGITLEX

%0:%0 LOOP ; 
1:1   LOOP ;
2:2   LOOP ;
3:3   LOOP ;
4:4   LOOP ;
5:5   LOOP ;
6:6   LOOP ;
7:7   LOOP ;
8:8   LOOP ;
9:9   LOOP ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!       N U M E R A L     L E X I C O N     E N D S     H E R E 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON SUBST

N-INFL-COMMON ;
%<pl%>:%>%{L%}%{A%}р N-INFL-COMMON ;

! NOTE: Some substantivised things might not have pl. form, numerals e.g.
! (not too sure, because in Tatar they have it:
! #1 meaning an approximate number:
     !# "Мин белгәндә аңа, мөгаен, _иллеләр_ тирәсе булгандыр."
! #2 <11 ones meaning pupils from a given class:
     !# өчләр = "pupils from the 3. class"
!
! If they don't take plural; link them to N-INFL-COMMON; if they do - to SUBST.
! That's the only difference between N-INFL-COMMON and SUBST.
!                                                                     /I.S./

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!!!!     CLITICS

! Modal/discourse particles

LEXICON CLIT-MODFOC

%+шы%<mod_foc%>:%>ш%{I%} # ;
# ;

LEXICON CLIT-QST-MA

%+ма%<qst%>:%>% %{M%}%{A%} # ;

LEXICON CLIT-QST

CLIT-QST-MA ;
%+мекен%<qst%>:% %{M%}екен # ;
%+ма% не%<qst%>:%>% %{M%}%{A%}% не # ; ! FIXME CHECK
!%+меді%<qst%>:% %{M%}еді # ;          ! FIXME CHECK
# ;

LEXICON CLIT-MODASS

%+ғой%<mod_ass%>:% %{G%}ой # ;
# ;

LEXICON CLIT-GHANA-ETC

%+%-ақ%<postadv%>:%-ақ # ; ! ""
%+ғана%<postadv%>:% %{G%}ана # ; ! "only"
%+да%<postadv%>:% %{D%}%{A%} # ; ! "1. even if"
%+да%<cnjcoo%>:% %{D%}%{A%} # ; ! "also"
# ;

LEXICON CLIT

CLIT-QST ;
CLIT-MODASS ;
CLIT-GHANA-ETC ;

LEXICON V-PERS-COP ! P_3

%+е%<cop%>: V-PERS-S1 ;

LEXICON CLIT-GENERAL ! Includes personal copula suffixes, therefore appears only
                     ! after nominal parts of speech (and only after some cases)
                     ! FIXME: to be checked whether leads to any overgeneration
CLIT ;
V-PERS-COP ;





!!!!!!!!!!!!!!!!!!!!!!     NOMINAL INFLECTION (N-INFL-COMMON)

! The following combinations are possible (examples in Tatar):
!
! stem > case                                        : әти<nom> : әтине<acc>
!
! stem > poss-real > case                            : әтием<px1sg><nom> : әтиемне<px1sg><acc>
! stem > poss-real > ныкы/дагы/дай(*1)               : әтиемнеке/китабындагы,өегездәге/әтиемдәй
! stem > poss-real > ныкы/дагы > case                : Монда _эчеңдәгене_ тышка чыгарырга ярамый. (источник: musulman.su) 
! stem > poss-real > [ныкы]/дагы > poss-real > case  : _Эчендәгесе_(*3) – тышында, диләр андый кешеләр турында. (источник: beznen.ru)
!
! stem > ныкы/дагы/дай                               : әтинеке/әтидәге/әтидәй
! stem > ныкы/дагы(*2) > case                        : Бу соңгы _еллардагының_ өчтән икесе чамасы.
!
! stem > сыз(abe)                  WORKS
! stem > сыз(abe) > case
! stem > сыз(abe) > poss > case    (?)
!
! *1. "ныкы" form in some Tatar grammars is called "predicative genitive".
!     Indeed, when not substantivised, it has predicative use:
!     "Бу яулык әнинеке." (vs "Әнинең яулыгы.")
! *2. "дай" is considered to be an adverbializer, so no case endings after it (=no <subst> reading).
!     (at least in Tatar, I think that in Kazakh it's the same)
! *3. This doesn't work atm (it's recursive), but it doesn't seem to be very frequent also;
!     Most likely it is restricted to px3sp.
!-------------------------------------------

LEXICON CASE-COMMON

%<gen%>:%>%{N%}%{I%}ң CLIT ;
%<dat%>:%>%{G%}%{A%} CLIT ;
%<acc%>:%>%{N%}%{I%} CLIT ;
%<ins%>:%>%{M%}ен CLIT ;

LEXICON CASE

CASE-COMMON ;
%<nom%>: CLIT-GENERAL ;
%<abl%>:%>%{D%}%{A%}н CLIT-GENERAL ;
%<loc%>:%>%{D%}%{A%} CLIT-GENERAL ;


LEXICON CASE-2  ! The difference from CASE is that CASE-2 doesn't continue
                ! with personal copula suffixes. Atm used only for <abe>
                ! thing /02082012, IS/

CASE-COMMON ;
%<nom%>: CLIT ;
%<abl%>:%>%{D%}%{A%}н CLIT ;
%<loc%>:%>%{D%}%{A%} CLIT ;

LEXICON ATTR/SUBST

%<attr%>: CLIT ;
%<subst%>: CASE ;
%<subst%>%<pl%>:%>%{L%}%{A%}р CASE ;

LEXICON ABE-ATTR/ADVL/SUBST

! <abe> thing doesn't take personal copula suffixes (JNW, please correct me),
! so SUBST reading of it is linked to CASE-2, not CASE.
! A counterexample could be e.g. "бәхет-сез-мен", but that's only because "бәхетсез"
! has lexicalised as an adjective.

! Note that allowing of the (scarcely existing) [X +<abe> +<pers.cop> (+ modalpcle)] sequence
! would require some additional rules in kaz-tat.t2x /02082012, IS/

%<attr%>: CLIT ;
%<advl%>: CLIT ;
%<subst%>: CASE-2 ;
%<subst%>%<pl%>:%>%{L%}%{A%}р CASE-2 ;

LEXICON Abessive          ! Doesn't appear after possessives

!%<abe%>:%>с%{I%}з ATTR/ADVL/SUBST ;
%+сыз%<post%>:%>с%{I%}з ABE-ATTR/ADVL/SUBST ;

LEXICON GENPOSS-ETC      ! Stuff which can appear after possessives too

%<px%>:%>%{N%}ікі%{n%} CASE ;
%<loc%>:%>%{D%}%{A%}%{G%}%{I%} ATTR/SUBST ; ! <s>FIXME:NOTE: I am in favour for changing the tag to loc2
                                            ! ("DAGI"=loc2 and not "DAGI"=loc"DA"+attr"GI") But first
                                            ! I'll give some arguments in a mail to apertium-turkic</s>
%<sim%>:%>%{D%}%{A%}й CLIT ;                ! FIXME:CHECK:In Tatar this is considered to be an adverbializer, so
                                            ! no case endings after it. In Kazakh it should be the same /I.S./ 
LEXICON CASE-ETC

CASE ;
GENPOSS-ETC ;
!%<adv%>:ш%{A%} # ;          ! FIXME: might not be quite the right place

LEXICON POSS-REAL

%<px1sg%>:%>%{I%}м CASE-ETC ;
%<px2sg%>:%>%{I%}ң CASE-ETC ;
%<px3sp%>:%>%{S%}%{I%}%{n%} CASE-ETC ;
%<px1pl%>:%>%{I%}м%{I%}з CASE-ETC ;
%<px2pl%>:%>%{I%}ңд%{A%}р CASE-ETC ;

%<px2sg%>%<frm%>:%>%{I%}ң%{I%}з CASE-ETC ;
%<px2pl%>%<frm%>:%{I%}ң%{I%}зд%{A%}р CASE-ETC ;

LEXICON POSSESSION ! I guess this was used once to implement some
                   ! recursive things, e.g. the sequence
                   ! [stem > poss-real > [ныкы]/дагы > poss-real > case]
                   ! Some gerunds link to it
POSS-REAL ;

LEXICON N-INFL-COMMON

POSSESSION ;
CASE-ETC ;
Abessive ; ! FIXME might lead to overgeneration:
           !! check whether it occurs after substantivized stuff;
           !! not all pronouns take it. "Синсез" vs "*үземсез".





!!!!!!!!!!!!!!!!!!!!!!     ADJECTIVE'S (ADJECTIVIAL) INFLECTION (ADJ-COMMON)

! FIXME: this section might need some refactoring
!! i.e. change <adj><comp><subst> to <adj><subst><comp>
!! and make use of ADJ-LEVELS class

LEXICON ADJ-LEVELS

%<comp%>:%>%{I%}р%{A%}%{K%} CLIT ;
CLIT ;

LEXICON A1                ! adjectives that can be both substantivised and andverbialised;
                          ! all three readings (<adj>, <adj.subst> and <adj.advl>) have comparison levels.
                          !# жақсы, тез

%<adj%>: CLIT ;                                       !# жақсы адам
%<adj%>%<comp%>:%>%{I%}р%{A%}%{K%} CLIT ;             !# жақсырақ іс

%<adj%>%<subst%>: SUBST ;                             !# жақсыны таптым 
%<adj%>%<comp%>%<subst%>:%>%{I%}р%{A%}%{K%} SUBST ;   !# жақсырақты таптым

%<adj%>%<advl%>: CLIT ;                               !# жақсы білемін
%<adj%>%<comp%>%<advl%>:%>%{I%}р%{A%}%{K%} CLIT ;     !# "...ион көп болса соғұрлым тоқ жақсырақ өтеді."

LEXICON A2                 ! (derived/not fully lexicalised) adjectives without adverbial reading;
                           ! <adj> and <adj.subst> readings have comparison levels.
                           !# ескі

%<adj%>: CLIT ;                                       !# ескі дос
%<adj%>%<comp%>:%>%{I%}р%{A%}%{K%} CLIT ;             !# ескірек заманда

%<adj%>%<subst%>: SUBST ;                             !# "Ол ескіні ертеден есінде сақтаған, жаңаның жалынды жаршысы болған ақын."
%<adj%>%<comp%>%<subst%>:%>%{I%}р%{A%}%{K%} SUBST ;   !# "Ихтимал, бу бина башкасы, искерәге урынында утырадыр."

LEXICON A3                 ! (derived/not fully lexicalised) adjectives without adverbial reading,
                           ! so-called "predicatives" (бар, жоқ);
                           ! no comparison levels at all.
                           !# көктемгі, басты, бар, жоқ

%<adj%>: CLIT ;                                       !# көктемгі су тасқыны
%<adj%>%<subst%>: SUBST ;                             !# "...Самаранч ең бастыны түсінді..."
                                                      !# "Аты бардың заты бар."

! FIXME:NOTE:loc.attr/subst form of some of them seems to be pure overgeneration:
! e.g. *язгыдагы

LEXICON A4                 ! "pure" adjectives - no adverbial and substantive readings, no comparison levels;
                           !# ұлттық

%<adj%>: CLIT ;                                       !# ұлттық тағам

LEXICON V-ADJ

# ;
%<subst%>: GER-INFL ; ! Dont't be confused by the name of the cont.class,
                      ! it's basically just a simplified N-INFL-COMMON

!!!!!!!!!!!!!!!!!!!!!!     VERBAL INFLECTION (V-INFL-COMMON)

LEXICON V-PERS-S1-NO3PERSON

%<p1%>%<sg%>:%>%{M%}%{I%}н CLIT ;
%<p2%>%<sg%>:%>с%{I%}ң CLIT ;
%<p1%>%<pl%>:%>%{M%}%{I%}з CLIT ;
%<p2%>%<pl%>:%>с%{I%}ңд%{A%}р CLIT ;

%<p2%>%<frm%>%<sg%>:%>с%{I%}з CLIT ;
%<p2%>%<frm%>%<pl%>:%>с%{I%}зд%{A%}р CLIT ;

LEXICON V-PERS-S1

! Used for: -AR-<fut>, -GAn-<past>, -MAK-<fut_plan>,

V-PERS-S1-NO3PERSON ;
%<p3%>%<sg%>: CLIT ;
%<p3%>%<pl%>: CLIT ;

LEXICON V-PERS-S2

! Used for: -DI-<ifi>, -sA-<prc_cond>, -sA-<gna_cond>

%<p1%>%<sg%>:%>м CLIT ;
%<p2%>%<sg%>:%>ң CLIT ;
%<p3%>%<sg%>: CLIT ;
%<p1%>%<pl%>:%>%{K%} CLIT ;
%<p2%>%<pl%>:%>ңд%{A%}р CLIT ;
%<p3%>%<pl%>: CLIT ;

%<p2%>%<frm%>%<sg%>:%>ң%{I%}з CLIT ;
%<p2%>%<frm%>%<pl%>:%>ң%{I%}зд%{A%}р CLIT ;

LEXICON V-PERS-AOR

! As the name indicates, used for -{E}-<aor>
! NOTE that: Алма театрға бара<aor><p3><sg> ма? – Алма в театр идёт?

V-PERS-S1-NO3PERSON ;
%<p1%>%<sg%>:%>м CLIT ;                    ! Dir/LR
%<p3%>%<sg%>:%>д%{I%} CLIT ;
%<p3%>%<sg%>: CLIT-QST-MA ;              ! Dir/LR
%<p3%>%<pl%>:%>д%{I%} CLIT ;
%<p3%>%<pl%>: CLIT-QST-MA ;              ! Dir/LR ;

LEXICON V-PERS-IFI_EVID

V-PERS-S1-NO3PERSON ;
%<p3%>%<sg%>:%>т%{I%} CLIT ;
%<p3%>%<sg%>:%>т%{I%} CLIT ;

LEXICON VOL-ENDINGS

%<p1%>%<sg%>:%>м CLIT-GHANA-ETC ;
%<p2%>%<sg%>:%>ң CLIT-GHANA-ETC ;
%<p3%>%<sg%>:%>с%{I%} CLIT-GHANA-ETC ;
%<p1%>%<pl%>:%>м%{I%}з CLIT-GHANA-ETC ;
%<p2%>%<pl%>:%>л%{A%}р%>%{I%}ң CLIT-GHANA-ETC ;
%<p3%>%<pl%>:%>с%{I%} CLIT-GHANA-ETC ;
%<p3%>%<pl%>:%>л%{A%}р%>%{I%} CLIT-GHANA-ETC ;             ! Dir/LR

%<p2%>%<frm%>%<sg%>:%>ң%{I%}з CLIT-GHANA-ETC ;
%<p2%>%<frm%>%<pl%>:%>л%{A%}р%>%{I%}ң%{I%}з CLIT-GHANA-ETC ;

!!!!!!!   G E R U N D S   I N F L E C T I O N

! This is a somewhat simplified version of N-INFL-COMMON.
! The idea was to avoid overgeneration (I don't think that ger+DAGI would take
! case endings e.g. Same for ger+NIKI). Still, this cont.class might be not
! suitable for all of the gerunds. /IS/

LEXICON GER-CASE-NO_COPULA

CASE-COMMON ;
%<nom%>: CLIT ;
%<abl%>:%>%{D%}%{A%}н CLIT ;
%<loc%>:%>%{D%}%{A%} CLIT ;

LEXICON GER-ATTR/SUBST

%<attr%>: CLIT ;
!%<subst%>: CASE ;
!%<subst%>%<pl%>:%>%{L%}%{A%}р CASE ;

LEXICON GER-ABE-ATTR/ADVL/SUBST

!%<attr%>: CLIT ;
%<advl%>: CLIT ;
!%<subst%>: CASE-2 ;
!%<subst%>%<pl%>:%>%{L%}%{A%}р CASE-2 ;

LEXICON GER-ABE-ETC          ! Stuff which doesn't appear after possessives

%+сыз%<post%>:%>с%{I%}з GER-ABE-ATTR/ADVL/SUBST ;

LEXICON GER-GENPOSS-ETC      ! Stuff which can appear after possessives too

%<px%>:%>%{N%}ікі%{n%} CLIT ;
%<loc%>:%>%{D%}%{A%}%{G%}%{I%} GER-ATTR/SUBST ;
%<sim%>:%>%{D%}%{A%}й CLIT ;

LEXICON GER-CASE-ETC

GER-CASE-NO_COPULA ;
GER-GENPOSS-ETC ;

LEXICON GER-POSS-REAL

%<px1sg%>:%>%{I%}м GER-CASE-ETC ;
%<px2sg%>:%>%{I%}ң GER-CASE-ETC ;
%<px3sp%>:%>%{S%}%{I%}%{n%} GER-CASE-ETC ;
%<px1pl%>:%>%{I%}м%{I%}з GER-CASE-ETC ;
%<px2pl%>:%>%{I%}ңд%{A%}р GER-CASE-ETC ;

%<px2sg%>%<frm%>:%>%{I%}ң%{I%}з GER-CASE-ETC ;
%<px2pl%>%<frm%>:%>%{I%}ң%{I%}зд%{A%}р GER-CASE-ETC ;

LEXICON GER-INFL

GER-POSS-REAL ;
GER-CASE-ETC ;
%<pl%>:%>%{L%}%{A%}р GER-POSS-REAL ;
%<pl%>:%>%{L%}%{A%}р GER-CASE-ETC ;
GER-ABE-ETC ;

!!!!!!!   end of gerunds inflection lexicon

LEXICON V-FINITE-IRREGULAR_NEGATIVE

%<fut%>:%>%{A%}р V-PERS-S1 ;
%<neg%>%<fut%>:%>м%{A%}с V-PERS-S1 ;

LEXICON V-FINITE-REGULAR_NEGATIVE

%<aor%>:%>%{E%} V-PERS-AOR ;
%<past%>:%>%{G%}%{A%}н V-PERS-S1 ;
%<ifi%>:%>%{D%}%{I%} V-PERS-S2 ;
%<ifi_evid%>:%>%{I%}п V-PERS-IFI_EVID ;
%<fut_plan%>:%>%{M%}%{A%}%{K%} V-PERS-S1 ;

LEXICON V-NONFINITE-IRREGULAR_NEGATIVE

! Participles
%<prc_perf%>:%>%{I%}п CLIT-GHANA-ETC ;
%<prc_perf%>:%>%{o%}%{I%}п CLIT-GHANA-ETC ;            ! Dir/LR
%<neg%>%<prc_perf%>:%>%{M%}%{A%}%>%{E%} CLIT-GHANA-ETC ;
%<prc_vol%>:%>%{G%}%{I%} VOL-ENDINGS ;
%<prc_fplan%>:%>%{M%}%{A%}%{K%} CLIT ;
%<prc_fplan%>:%>%{M%}%{A%}%{K%}ш%{I%} CLIT ;           ! Dir/LR
%<prc_plan%>:%>%{G%}%{A%}л%{I%} CLIT-GHANA-ETC ;

! Verbal adverbs
%<gna_perf%>:%>%{I%}п CLIT ;
%<neg%>%<gna_perf%>:%>%{M%}%{A%}%>%{E%} CLIT ;

%<gna_until%>:%>%{G%}%{A%}нш%{A%} CLIT ;
%<gna_after%>:%>%{G%}%{A%}л%{I%} CLIT ;

! Verbal Adjectives
%<gpr_fut%>:%>%{A%}р V-ADJ ;
%<neg%>%<gpr_fut%>:%>%{M%}%{A%}%>%с V-ADJ ;

%<gpr_ppot%>:%>%{A%}р%>л%{I%}к # ;
%<neg%>%<gpr_ppot%>:%>%{M%}%{A%}%>сл%{I%}%{K%} # ;

!%<neg%>%<gpr_pot2%>:%>%{G%}%{I%}с # ; FIXME looks wrong

! Gerunds
%<ger_fut%>:%>%{A%}р GER-POSS-REAL ;
%<neg%>%<ger_fut%>:%>%{M%}%{A%}%>с GER-POSS-REAL ;

%<ger_ppot%>:%>%{A%}р%>л%{I%}к GER-POSS-REAL ;
%<neg%>%<ger_ppot%>:%>%{M%}%{A%}%>сл%{I%}%{K%} GER-POSS-REAL ;

LEXICON V-NONFINITE-REGULAR_NEGATIVE

! Participles
%<prc_impf%>:%>%{E%} CLIT-GHANA-ETC ;
%<prc_cond%>:%>с%{A%} V-PERS-S2 ;

! Verbal adverbs
%<gna_cond%>:%>с%{A%} V-PERS-S2 ;

! Verbal adjectives
%<gpr_past%>:%>%{G%}%{A%}н V-ADJ ;
%<gpr_impf%>:%>%{E%}т%{I%}н V-ADJ ;
%<gpr_pot%>:%>у%>ш%{I%} V-ADJ ;

! Gerunds
%<ger%>:%>у GER-INFL ;
%<ger_past%>:%>%{G%}%{A%}н GER-INFL ;
%<ger_perf%>:%>%{G%}%{A%}н%>%{L%}%{I%}%{K%} GER-INFL ;
%<ger_abs%>:%>у%>ш%{I%}%>%{L%}%{I%}%{K%} GER-INFL ;
%<ger_impf%>:%>%{E%}т%{I%}н GER-POSS-REAL ;

! Imperatives
%<imp%>%<p2%>%<sg%>: CLIT-MODFOC ;
%<imp%>%<p2%>%<pl%>:%>%{I%}ң%>д%{A%}р CLIT-MODFOC ;
%<imp%>%<p2%>%<frm%>%<sg%>:%>%{I%}ң%{I%}з CLIT-MODFOC ;
%<imp%>%<p2%>%<frm%>%<sg%>:%>%{I%}ң CLIT-MODFOC ;     ! Dir/LR FIXME CHECK
%<imp%>%<p2%>%<frm%>%<pl%>:%>%{I%}ң%{I%}з%>д%{A%}р CLIT-MODFOC ;

! Optative/jussive forms
%<opt%>%<p1%>%<sg%>:%>%{A%}й%{I%}н CLIT ;
!%<opt%>%<p2%>%<sg%>:%>%{G%}%{I%}н CLIT ;              ! FIXME check
%<opt%>%<p3%>%<sg%>:%>с%{I%}н CLIT ;
%<opt%>%<p1%>%<pl%>:%>%{A%}й%{I%}%{K%} CLIT ;
!%<opt%>%<p2%>%<pl%>:%>%{G%}%{I%}л%{A%} CLIT ;         ! FIXME check
%<opt%>%<p3%>%<pl%>:%>с%{I%}н CLIT ;

!%<opt%>%<p2%>%<frm%>%<sg%>:%>%{G%}%{I%}л%{A%} CLIT ;  ! FIXME check
!%<opt%>%<p2%>%<frm%>%<pl%>:%>%{G%}%{I%}л%{A%} CLIT ;  ! FIXME check

LEXICON V-COMMON

V-FINITE-REGULAR_NEGATIVE ;
%<neg%>:%>%{M%}%{A%} V-FINITE-REGULAR_NEGATIVE ;

V-FINITE-IRREGULAR_NEGATIVE ;

V-NONFINITE-REGULAR_NEGATIVE ;
%<neg%>:%>%{M%}%{A%} V-NONFINITE-REGULAR_NEGATIVE ;

V-NONFINITE-IRREGULAR_NEGATIVE ;

LEXICON V-DER

%<coop%>:%>%{I%}с V-COMMON ;

LEXICON V-TV-NOPASS

%<v%>%<tv%>: V-COMMON ;
%<v%>%<tv%>: V-DER ;

LEXICON V-TV-PASS

! e.g. көрін
%<v%>%<tv%>%<pass%>: V-COMMON ;
%<v%>%<tv%>%<pass%>: V-DER;

LEXICON V-TV

%<v%>%<tv%>: V-COMMON ;
%<v%>%<tv%>: V-DER ;
%<v%>%<tv%>%<pass%>:%>%{I%}%{l%} V-COMMON ;

LEXICON V-IV

%<v%>%<iv%>: V-COMMON ;
%<v%>%<iv%>: V-DER ;

!!!!!!!!!!!!!!!!!!!!!!     auxiliary verbs

LEXICON Vinfl-AUX-IMPF

%<vaux%>%<pres%>: V-PERS-S1 ; ! /жүр, жатыр, тұр, отыр/

LEXICON Vinfl-AUX

%<vaux%>: V-COMMON ; ! these four verbs in other tenses and all other aux. verbs

!!!!!!!!!!!!!!!!!!!!!!     copula

LEXICON Copula

е%<cop%>%<ger_past%>:екен GER-POSS-REAL ;
е%<cop%>%<ger_perf%>:екен%>%{L%}%{I%}к GER-POSS-REAL ;

е%<cop%>%<neg%>:емес V-PERS-S1 ;
е%<cop%>%<past%>:екен V-PERS-S1 ;
е%<cop%>%<neg%>%<past%>:емес% екен V-PERS-S1 ;
е%<cop%>%<ifi%>:е%>%{D%}%{I%} V-PERS-S2 ;

!!!!!!!!!!!!!!!!!!!!!!     PRONOUN'S INFLECTION

!!!!!!!   P E R S O N A L

LEXICON PRON-12SG-CASES

%<gen%>:енің CLIT ;
%<dat%>:аған CLIT ;
%<acc%>:ені CLIT ;
%<abl%>:енен CLIT ;
%<loc%>:енде CLIT ;
%<ins%>:енімен CLIT ;
%<px%>:енікі%{n%} CASE ;
%<loc%>:ендегі ATTR/SUBST ;
%<sim%>:ендей CLIT ;
%<adv%>:еніңше CLIT ;

LEXICON PRON-PERS-INFL

CASE-ETC ;

LEXICON PersonalPronouns

мен%<prn%>%<pers%>%<p1%>%<sg%>%<nom%>:мен CLIT ;
мен%<prn%>%<pers%>%<p1%>%<sg%>:м PRON-12SG-CASES ;

сен%<prn%>%<pers%>%<p2%>%<sg%>%<nom%>:сен CLIT ;
сен%<prn%>%<pers%>%<p2%>%<sg%>:с PRON-12SG-CASES ;

ол%<prn%>%<pers%>%<p3%>%<sg%>%<nom%>:ол CLIT ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<nom%>:о CLIT ; ! Dir/LR
ол%<prn%>%<pers%>%<p3%>%<sg%>%<gen%>:оның CLIT ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<dat%>:оған CLIT ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<acc%>:оны CLIT ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:одан CLIT ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:онда CLIT ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:онымен CLIT ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<px%>:онікі%{n%} CASE ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:ондағы ATTR/SUBST ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<sim%>:ондай CLIT ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<adv%>:оныңша CLIT ;

біз%<prn%>%<pers%>%<p1%>%<pl%>:біз PRON-PERS-INFL ;
біз%<prn%>%<pers%>%<p1%>%<pl%>%<adv%>:біздіңше CLIT ;

сендер%<prn%>%<pers%>%<p2%>%<pl%>:сендер PRON-PERS-INFL ;
сендер%<prn%>%<pers%>%<p2%>%<pl%>%<adv%>:сендерше CLIT ;

олар%<prn%>%<pers%>%<p3%>%<pl%>:олар PRON-PERS-INFL ;
олар%<prn%>%<pers%>%<p3%>%<pl%>%<adv%>:оларша CLIT ;

сіз%<prn%>%<pers%>%<p2%>%<sg%>%<frm%>:сіз PRON-PERS-INFL ;
сіз%<prn%>%<pers%>%<p2%>%<sg%>%<frm%>%<adv%>:сіздіңше CLIT ;
сіздер%<prn%>%<pers%>%<p2%>%<pl%>%<frm%>:сіздер PRON-PERS-INFL ;
сіздер%<prn%>%<pers%>%<p2%>%<pl%>%<frm%>%<adv%>:сіздерше CLIT ;

!!!!!!!   D E M O N S T R A T I V E

! FIXME CHECK (CLIT-GENERAL vs CLIT) I can't imagine forms like "бумын, бусың" etc.
! in Tatar, but not sure how it works in Kazakh /I.S./
!-----
! Good idea is to link prn.dem.sim forms alternatively to CLIT and CASE,
! so that it's "adverbial reading" (e.g. "шундый каты кычкырды") doesn't
! receive <nom> tag /IS/

LEXICON PRON-DEM-INFL

%<gen%>:ның CLIT ;
%<dat%>:ған CLIT ;
%<acc%>:ны CLIT ;
%<abl%>:дан CLIT-GENERAL ;
%<abl%>:нан CLIT-GENERAL ; ! Dir/LR FIXME actually only "осы" seems to take this.
%<loc%>:нда CLIT-GENERAL ;
%<px%>:нікі%{n%} CASE ;
%<loc%>:ндагы ATTR/SUBST ;
%<sim%>:ндай CASE ;
%<adv%>:лай CLIT-GENERAL ;
%<qnt%>:нша CLIT ;         !

%<pl%>:%>%{L%}%{A%}р CASE-ETC ;
%<pl%>%<px3sp%>:%>%{L%}%{A%}р%{I%}%{n%} CASE ;

! Commented out till these forms get implemented in tat.lexc too
!%<TD%>:шалық CLIT ;        ! "that much"  ! FIXME: check cont lex
!%<reas%>%<abl%>:ндықтан # ; ! "because of"  ! FIXME: not attested with осы?

! FIXME: 1) link the below to PRON-DEM-INFL if possible
!            (later, once we are certain about things)

LEXICON PRON-DEM-BUL

! FIXME CHECK (бұны vs мұны) Grammars I have list only forms with "б", but variants
! starting with "м" seem to be more frequent in the (wikipedia) corpus /I.S./
!-----
! <s>FIXME</s> (бұл%<prn%>%<dem%>%<pl%>:бұлар N-INFL-COMMON ;) N-INFL-COMMON might lead to
! overgeneration with p1,p2 possessives (they seem to take onle <px3sp> ending);
! on the other hand, with something like бу%<prn%>%<dem%>%<px3sp%>:монысы%{n%} CASE ;
! we loose things like бу<prn><dem><pl><px>:боларныкы /I.S./

бұл%<prn%>%<dem%>%<nom%>:бұл CLIT-GENERAL ;
бұл%<prn%>%<dem%>%<nom%>:бұ CLIT-GENERAL ; ! Dir/LR
бұл%<prn%>%<dem%>%<gen%>:бұның CLIT ;
бұл%<prn%>%<dem%>%<gen%>:мұның CLIT ; ! Dir/LR
бұл%<prn%>%<dem%>%<dat%>:бұған CLIT ;
бұл%<prn%>%<dem%>%<acc%>:бұны CLIT ;
бұл%<prn%>%<dem%>%<acc%>:мұны CLIT ; ! Dir/LR
бұл%<prn%>%<dem%>%<abl%>:бұдан CLIT-GENERAL ;
бұл%<prn%>%<dem%>%<abl%>:мұнан CLIT-GENERAL ; ! Dir/LR
бұл%<prn%>%<dem%>%<loc%>:бұнда CLIT-GENERAL ;
бұл%<prn%>%<dem%>%<loc%>:мұнда CLIT-GENERAL ; ! Dir/LR
бұл%<prn%>%<dem%>%<ins%>:бұнымен CLIT ;
бұл%<prn%>%<dem%>%<ins%>:мұнымен CLIT ; ! Dir/LR
бұл%<prn%>%<dem%>%<px%>:мұнікі%{n%} CASE ; ! FIXME: check "м" vs "б"
бұл%<prn%>%<dem%>%<loc%>:бұндағы ATTR/SUBST ;
бұл%<prn%>%<dem%>%<loc%>:мұндағы ATTR/SUBST ; ! Dir/LR
бұл%<prn%>%<dem%>%<sim%>:мұндай CASE ;
бұл%<prn%>%<dem%>%<sim%>:бұндай CASE ; ! Dir/LR
бұл%<prn%>%<dem%>%<sim%>%<px3sp%>:мұндай%{I%}%{n%} CASE ;
бұл%<prn%>%<dem%>%<sim%>%<pl%>:мұндайлар CASE ;
бұл%<prn%>%<dem%>%<sim%>%<pl%>%<px3sp%>:мұндайлары%{n%} CASE ;
бұл%<prn%>%<dem%>%<adv%>:былай CLIT-GENERAL ;
бұл%<prn%>%<dem%>%<adv%>:бұлай CLIT-GENERAL ; ! Dir/LR
бұл%<prn%>%<dem%>%<qnt%>:мұнша CLIT ;
бұл%<prn%>%<dem%>%<px3sp%>:бұнысы%{n%} CASE ;

бұл%<prn%>%<dem%>%<pl%>:бұлар CASE-ETC ;
бұл%<prn%>%<dem%>%<pl%>%<px3sp%>:бұлары%{n%} CASE ;

LEXICON PRON-DEM-MINA

мына%<prn%>%<dem%>%<nom%>:мына CLIT-GENERAL ;
мына%<prn%>%<dem%>%<gen%>:мынаның CLIT ;
мына%<prn%>%<dem%>%<dat%>:мынаған CLIT ;
мына%<prn%>%<dem%>%<acc%>:мынаны CLIT ;
мына%<prn%>%<dem%>%<abl%>:мынадан CLIT-GENERAL ;
мына%<prn%>%<dem%>%<loc%>:мынада CLIT-GENERAL ;
мына%<prn%>%<dem%>%<ins%>:мынамен CLIT ;
!мына%<prn%>%<dem%>%<px%>:мынанікі%{n%} CASE ;
!мына%<prn%>%<dem%>%<loc%>:мынадағы ATTR/SUBST ;
мына%<prn%>%<dem%>%<sim%>:мынадай CASE ;
мына%<prn%>%<dem%>%<adv%>:мыналай CLIT-GENERAL ;
мына%<prn%>%<dem%>%<px3sp%>:мынасы%{n%} CASE ;

мына%<prn%>%<dem%>%<pl%>:мыналар CASE-ETC ;
мына%<prn%>%<dem%>%<pl%>%<px3sp%>:мыналары%{n%} CASE ;

LEXICON PRON-DEM-ANA

ана%<prn%>%<dem%>%<nom%>:ана CLIT-GENERAL ;
ана%<prn%>%<dem%>%<gen%>:ананың CLIT ;
ана%<prn%>%<dem%>%<dat%>:анаған CLIT ;
ана%<prn%>%<dem%>%<acc%>:ананы CLIT ;
ана%<prn%>%<dem%>%<abl%>:анадан CLIT-GENERAL ;
ана%<prn%>%<dem%>%<loc%>:анада CLIT-GENERAL ;
ана%<prn%>%<dem%>%<ins%>:анамен CLIT ;
!ана%<prn%>%<dem%>%<px%>:ананікі%{n%} CASE ;
!ана%<prn%>%<dem%>%<loc%>:анадағы ATTR/SUBST ;
ана%<prn%>%<dem%>%<sim%>:анадай CASE ;
!ана%<prn%>%<dem%>%<adv%>:аналай CLIT-GENERAL ;
!ана%<prn%>%<dem%>%<px3sp%>:анасы%{n%} CASE ;

ана%<prn%>%<dem%>%<pl%>:аналар CASE-ETC ;
ана%<prn%>%<dem%>%<pl%>%<px3sp%>:аналары%{n%} CASE ;

LEXICON DemonstrativePronouns

PRON-DEM-BUL ;
PRON-DEM-MINA ;
PRON-DEM-ANA ;

сол%<prn%>%<dem%>%<nom%>:сол CLIT-GENERAL ;
сол%<prn%>%<dem%>:со PRON-DEM-INFL ;
сол%<prn%>%<dem%>%<ins%>:сонымен CLIT ;
сол%<prn%>%<dem%>%<px3sp%>:сонысы%{n%} CASE ;

ол%<prn%>%<dem%>%<nom%>:ол CLIT ;
ол%<prn%>%<dem%>:о PRON-DEM-INFL ;
ол%<prn%>%<dem%>%<ins%>:онымен CLIT ;
ол%<prn%>%<dem%>%<px3sp%>:онысы%{n%} CASE ;

осы%<prn%>%<dem%>%<nom%>:осы CLIT ;
осы%<prn%>%<dem%>:осы PRON-DEM-INFL ;
осы%<prn%>%<dem%>%<ins%>:осымен CLIT ;
осы%<prn%>%<dem%>%<px3sp%>:осысы%{n%} CASE ;

! FIXME DISCUSS: Since <reas><abl> is a feature only of "ol" and "sol",
! below were going to be lexicalised and added as subordinative
! conjunctions. /IS/
! This is a bad idea though, because -ндықтан affix is a feature of verbs
! as well, so we'll have to consider it a productive derivation anyway /IS/

ол%<prn%>%<dem%>%<reas%>%<abl%>:ондықтан # ; ! "because of that"
сол%<prn%>%<dem%>%<reas%>%<abl%>:сондықтан # ; ! "because of that"


!!!!!!!   I N T E R R O G A T I V E

! Some of the words listed in the grammars I have as interrogative pronouns
! were placed in other root lexicons depending on what POS they substitute or
! act like syntactically. So (as planned for today):
! 1) "неше" is a <num><itg>;
! 2) "қайда, қайдан, қашан, қалай, қалайша" are <adv><itg> (the first three
! should link to a class containing location cases btw, as many current adverbs
! do);
! 3) "қанша" is added both as an interrogative pronoun and as an interrogative
! adverb /IS/
!
! What already added as interrogative pronouns are: кім, ни, нәрсе
! And to be added as interrogative pronouns are: қайсы, қандай, қанша.
! I'd like to mark қандай as <sim> form (with қандай for the upper side though),
! but wouldn't go too deeply into the structure of other two and not derive them
! from "қай" (that's what they have most likely originated from) IS
!
! Of course, "қай", "қайсы" and all other pronouns which can be used as an
! attribute were added as determiners linking to CLIT as well.
! Good idea is to have an rlx rule selecting determiner reading when a word is
! ambiguous between (prn X nom) and (det X) and there is noun to the right;
! and selecting pronoun if there is a verb to the right.

LEXICON PRON-ITG-INFL

%<prn%>%<itg%>: GER-INFL ;
%<prn%>%<itg%>%<pl%>:%>%{L%}%{A%}р GER-INFL ;

LEXICON PRON-ITG-QAYSI

қайсы%<prn%>%<itg%>%<nom%>:қайсы CLIT ;
қайсы%<prn%>%<itg%>%<gen%>:қайсының CLIT ;
қайсы%<prn%>%<itg%>%<dat%>:қайсыған CLIT ;
қайсы%<prn%>%<itg%>%<acc%>:қайсыны CLIT ;
қайсы%<prn%>%<itg%>%<abl%>:қайсыдан CLIT ;
қайсы%<prn%>%<itg%>%<loc%>:қайсыда CLIT ;
қайсы%<prn%>%<itg%>%<ins%>:қайсымен CLIT ;

қайсы%<prn%>%<itg%>%<px%>:қайсынікі%{n%} CASE ;
қайсы%<prn%>%<itg%>:қайсы GER-POSS-REAL ; ! қайсысы, қайсыбыз, қайсығыз

қайсы%<prn%>%<itg%>%<pl%>:қайсылар GER-CASE-ETC ;
қайсы%<prn%>%<itg%>%<pl%>%<px3sp%>:қайсылары%{n%} GER-CASE-NO_COPULA ;
! ^ кайсылары(б/г)ыз are rare but possible in Tatar, so maybe these forms should
! be included too /IS/

LEXICON PRON-ITG-QANSHA

! Might be not complete (қаншалар?)
! Currently қанша<prn><itg> is translated with ничә<num><itg><subst> and
! қанша<adv><itg> is going to be translated with "күпме" or "никадәр"

қанша%<prn%>%<itg%>:қанша CASE ;

LEXICON PRON-ITG-QANDAY

! Might be not complete (қандайларығыз? and possessive stuff in general)

қандай%<prn%>%<itg%>%<sim%>:қандай CLIT ;
! ^ FIXME Similative form of demonstrative pronouns should receive an alternati-
! ve continuation with CLIT too. Consider e,g.: "Ең жақсы Интернет қандай?" Here
! it shouldn't receive <nom> tag I think /IS/
қандай%<prn%>%<itg%>%<sim%>:қандай CASE ;
қандай%<prn%>%<itg%>%<sim%>%<px3sp%>:қандайы%{n%} CASE ;
қандай%<prn%>%<itg%>%<sim%>%<pl%>:қандайлар CASE ;
қандай%<prn%>%<itg%>%<sim%>%<pl%>%<px3sp%>:қандайлары%{n%} CASE ;

LEXICON InterrogativePronouns

PRON-ITG-QAYSI ;
PRON-ITG-QANSHA ;
PRON-ITG-QANDAY ;
нәрсе:нәрсе PRON-ITG-INFL ; ! "thing" ! Use/MT
не:не PRON-ITG-INFL ; ! "what"
кім:кім PRON-ITG-INFL ; ! "who"

LEXICON PRON-QNT

%<prn%>%<qnt%>: GER-INFL ;

LEXICON PRON-IND

%<prn%>%<ind%>: GER-INFL ;

LEXICON PRON-IND-POSS

%<prn%>%<ind%>: POSSESSION ;

LEXICON PRNNEG

%<prn%>%<neg%>: CASE ;

LEXICON PRON-RECIP

%<prn%>%<recip%>: POSS-REAL ;

LEXICON PRON-REF-ADV

CASE-ETC ;
%<adv%>:ше CLIT ;

LEXICON PRON-REF

! FIXME JNW please have a look at this
! Listing all forms here because of overgeneration with *өздерім,
! *өздеріміз, *өзіңдер etc while linking to POSS-REAL. This was the idea.
! Now they are handled more like personal pronouns (the same tags in the same
! order), so they should pass transfer without any trouble.
!-----
! A remaining problem with reflexive pronoun is that it takes personal copula
! suffixes "selectively": e.g. prn.refl.px1sg would take only cop.px1sg
! (prn.ref.px3sp seems to take all of them though).
! A little bit of overgeneration won't hurt anyone :), but I think that odd
! forms could be filtered out somehow later. Don't know how to do it./04Aug, IS/

! FIXME DISCUSS Write full forms for the lower side and put only PRON-DEF in the
! Root Lexicon

%<prn%>%<ref%>%<p1%>%<sg%>:%>%{I%}м PRON-REF-ADV ;
%<prn%>%<ref%>%<p2%>%<sg%>:%>%{I%}ң PRON-REF-ADV ;
%<prn%>%<ref%>%<p3%>%<sg%>:%>%{S%}%{I%}%{n%} PRON-REF-ADV ;
%<prn%>%<ref%>%<p1%>%<pl%>:%>%{I%}м%{I%}з PRON-REF-ADV ;
%<prn%>%<ref%>%<p2%>%<pl%>:%>д%{A%}р%>%{I%}ң PRON-REF-ADV ;
%<prn%>%<ref%>%<p3%>%<pl%>:%>д%{A%}р%>%{S%}%{I%}%{n%} PRON-REF-ADV ;

%<prn%>%<ref%>%<p2%>%<sg%>%<frm%>:%>%{I%}ң%{I%}з PRON-REF-ADV ;
%<prn%>%<ref%>%<p2%>%<pl%>%<frm%>:%>д%{A%}р%>%{I%}ң%{I%}з PRON-REF-ADV ;

! This is how it was before:
!%<prn%>%<ref%>: POSS-REAL ;
!%<prn%>%<ref%>%<pl%>:%{L%}%{A%}р POSS-REAL ;

!!!!!!!!!!!!!!!!!!!!!!     DETERMINERS

LEXICON DET-DEM

%<det%>%<dem%>: # ;

LEXICON DET-QNT

%<det%>%<qnt%>: # ;

LEXICON DET-IND

%<det%>%<ind%>: # ;

LEXICON DET-ITG

%<det%>%<itg%>: # ;

LEXICON DET-NEG

%<det%>%<neg%>: # ;

LEXICON DET-REF

%<det%>%<ref%>: # ;


!!!!!!!!!!!!!!!!!!!!!!     OTHER PART-OF-SPEECH LEXICONS

LEXICON N1

%<n%>%<attr%>: # ;
%<n%>: SUBST ;

LEXICON N-COMPOUND-PX ! consider "N2" for the name
                      ! equiv of kir:LEXICON N-INFL-3PX-COMPOUND

%<n%>:%>%{S%}%{I%}%{n%} CASE-ETC ;
%<n%>: POSS-REAL ;     ! for stuff like "біздің ауа райымыз" and even "оның ауа райы", which should have a <px3sp> analysis

LEXICON N3
! Singularia tantum

%<n%>%<attr%>: # ;
%<n%>: N-INFL-COMMON ;

LEXICON N5
! This is only really needed for Russian noun stems where the last vowel is и or у.

:%{☭%} N1 ;


!LEXICON NAT
! Nationalites

!A1 ;              ! nationality's adjectival form, e.g. "Turkish [food]" ! Consider <n.attr> reading for this 
!N1 ;              ! a member of the nationality, e.g. "A Turk"
!:%>ш%{A%} ADV ;   ! as / in the style of the nat., e.g. "à la Turk"
!:%>ш%{A%} ADV ;   ! in the language of the nationality, e.g. "in Turkish" ! I guess ADJ was meant /I.S./
!:%>ш%{A%} N1 ;    ! the language of the nationality, e.g. "Turkish"

LEXICON ADV-LANG

N1 ;
ADV ;

LEXICON NP-COMMON
N-INFL-COMMON ;

LEXICON NP-TOP

%<np%>%<top%>: NP-COMMON ;
%<np%>%<top%>%<attr%>: # ;

!%<adj%>%<top%>:%{L%}%{I%}%{K%} ADJ-COMMON ;  ! FIXME: do these tags make sense?

LEXICON NP-ANT-M

%<np%>%<ant%>%<m%>: NP-COMMON ;
%<np%>%<ant%>%<m%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-ANT-F

! This isn't used at the moment,
! but e.g. ?Polish cognoms ending with -ска would take this cont.class

%<np%>%<ant%>%<f%>: NP-COMMON ;
%<np%>%<ant%>%<f%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-COG-M
 
%<np%>%<cog%>%<m%>: NP-COMMON ; 
%<np%>%<cog%>%<m%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-COG-MF

! Dual-gender cognoms.
! Don't derive anything /I.S./
 
%<np%>%<cog%>%<mf%>: NP-COMMON ; 
%<np%>%<cog%>%<mf%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-COG-OBIN-FEM

%<np%>%<cog%>%<f%>:а NP-COMMON ;
%<np%>%<cog%>%<f%>%<pl%>:а%>%{L%}%{A%}р NP-COMMON ;


LEXICON NP-COG-OB

! For cognoms ending with -ов, -ев
! They derive feminine equivalets taking -а /I.S./
! Also, even the ев ones are normally pronounced
!    (and take harmony) as if they end in йыф/йып

%<np%>%<cog%>%<m%>:%{☭%} NP-COMMON ;                       ! Dir/LR
%<np%>%<cog%>%<m%>%<pl%>:%{☭%}%>%{L%}%{A%}р NP-COMMON ;    ! Dir/LR

%<np%>%<cog%>%<m%>:%{а%}%{с%} NP-COMMON ;
%<np%>%<cog%>%<m%>%<pl%>:%{а%}%{с%}%>%{L%}%{A%}р NP-COMMON ;

NP-COG-OBIN-FEM ;


LEXICON NP-COG-IN

! For cognoms ending with -ов, -ев, -ин
! They derive feminine equivalets taking -а /I.S./

%<np%>%<cog%>%<m%>:%{☭%} NP-COMMON ;
%<np%>%<cog%>%<m%>%<pl%>:%{☭%}%>%{L%}%{A%}р NP-COMMON ;

NP-COG-OBIN-FEM ;


LEXICON NP-PAT-VICH

! For patronyms ending with -вич
! Their feminine equivalets end with -вна /I.S./

%<np%>%<pat%>%<m%>:вич%{☭%} NP-COMMON ;

%<np%>%<pat%>%<f%>:вна NP-COMMON ;

LEXICON NP-ORG

%<np%>%<org%>: CASE ; 

LEXICON NP-AL

%<np%>%<al%>: CASE ; 

LEXICON NUM-COMMON

%<num%>: # ;
%<num%>%<subst%>: SUBST ;

LEXICON NUM

NUM-COMMON ;
%<num%>%<ord%>:%>%{I%}нш%{I%} N-INFL-COMMON ;

LEXICON NUM-TWENTY

NUM-COMMON ;
%<num%>%<ord%>:%>с%{I%}нш%{I%} N-INFL-COMMON ;

LEXICON NUM-ITG ! This is a temporal solution
                ! (could be integrated with the above)

%<num%>%<itg%>: # ;
%<num%>%<itg%>%<subst%>: SUBST ;
%<num%>%<itg%>%<ord%>:%>%{I%}нш%{I%} N-INFL-COMMON ;

LEXICON NUM-COLL

%<num%>%<coll%>%<subst%>: N-INFL-COMMON ;
%<num%>%<coll%>%<advl%>: # ;

LEXICON NUM-ROMAN

%<num%>%<ord%>: # ;

LEXICON DIGITLEX

%<num%>: # ;

LEXICON POST

%<post%>: CLIT ;

LEXICON POSTADV

%<postadv%>: # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON KI

%<attr%>:%>%{G%}%{I%} CLIT ;

LEXICON ADV

%<adv%>: CLIT ;

LEXICON ADV-WITH-KI

ADV ;
%<adv%>: KI ;

LEXICON ADV-WITH-KI-I     ! Used only for бері<adv>/бергі<adv><attr> right now

:і ADV ;
%<adv%>: KI ;

LEXICON ADVITG

%<adv%>%<itg%>: # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON CC

%<cnjcoo%>: # ;

LEXICON CS

%<cnjsub%>: # ; 

LEXICON CA

%<cnjadv%>: # ; 

LEXICON QST

%<qst%>: # ;

LEXICON MOD-ASS

%<mod_ass%>: # ;

LEXICON MOD

! шығар, сияқты etc.

%<mod%>: # ;

LEXICON INTERJ

%<ij%>: # ;

LEXICON IDEO

%<ideo%>: # ;

LEXICON ABBR

%<abbr%>: # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON ModalParticles

ма:%~ма QST ; ! "" Dir/RL
ма:ма QST ; ! "" Dir/LR
ма:ба QST ; ! "" Dir/LR
ма:па QST ; ! "" Dir/LR
ма:ме QST ; ! "" Dir/LR
ма:бе QST ; ! "" Dir/LR
ма:пе QST ; ! "" Dir/LR

ғой:%~ғой MOD-ASS ; ! "" Dir/RL
ғой:ғой MOD-ASS ; ! "" Dir/LR
ғой:қой MOD-ASS ; ! "" Dir/LR

шығар:шығар MOD ; ! FIXME actually a verb, takes personal suffixes (V-PERS-S1);
                  ! requires some transfer rules as e.g. in the following:
                  !# Олар мүмкiн көп кiтап оқыған шығар.
                  !# Алар бәлки күп китап укыганнардыр.


!====================!
 LEXICON Conjunctions

және:және CC ; ! "and"
және:жөне CC ; ! "and" Dir/LR FIXME CHECK
ал:ал CC ; ! "contrastive and"
бірақ:бірақ CC ; ! "but"

 LEXICON Postpositions

арқылы:арқылы POST ; ! "through, via, by means of" + NOM
үшін:үшін POST ; ! "for (the benefit of), in order" + NOM
дейін:дейін POST ; ! "until, up to, as far as" + DAT
шейін:шейін POST ; ! "until, up to, as far as = дейін" + DAT

 LEXICON Postadverbs

ғана:%~ғана POSTADV ; ! Dir/RL
ғана:ғана POSTADV ;   ! "only" Dir/LR
ғана:қана POSTADV ;   ! "only" Dir/LR
да:%~да POSTADV ; ! "1. even if"     Dir/RL
да:да POSTADV ; ! ""       Dir/LR
да:де POSTADV ; ! ""       Dir/LR
да:та POSTADV ; ! ""       Dir/LR
да:те POSTADV ; ! ""       Dir/LR

 LEXICON Pronouns
!================!

PersonalPronouns ;

DemonstrativePronouns ;

InterrogativePronouns ;

бірдеме:бірдеме PRON-IND ; ! "something"
басқа:басқа PRON-IND ; ! "other"
! бәрі PRON-IND ; ! "everyone"  !!! This is an alternate of below ! FIXME: true for Kazakh too?
бәрі:бәр PRON-IND-POSS ; ! "everyone"  ! FIXME: in the end, this really shouldn't create <px*> tags too (same problem for kymorph)
кей:кей PRON-IND ; ! "some" FIXME CHECK

барлық:барлық PRON-QNT ; ! ""
біреу:біреу PRON-QNT ; ! ""
әркім:әркім PRON-QNT ; ! ""

ешкім:ешкім PRNNEG ; ! ""
ешнәрсе:ешнәрсе PRNNEG ; ! ""
еш% нәрсе:еш% нәрсе PRNNEG ; ! "" Use/MT
ештеңе:ештеңе PRNNEG ; ! ""

өз:өз PRON-REF ; ! "self"

бири%-бир:бири%-бир PRON-RECIP ; ! "each other, one another"


!===================!
 LEXICON Determiners
!===================!


анау:анау DET-DEM ;
әлгі:әлгі DET-DEM ; ! "~этот"
бұл:бұл DET-DEM ;
ол:ол DET-DEM ; 
мынау:мынау DET-DEM ;
ол:ол DET-DEM ;
осы:осы DET-DEM ;
сол:сол DET-DEM ;
сонау:сонау DET-DEM ;
осынау:осынау DET-DEM ;

әр:әр DET-QNT ; ! "every"
әрбір:әрбір DET-QNT ; ! "each"
әрбір:һәрбір DET-QNT ; ! "each" Dir/LR
әртүрлі:әртүрлі DET-QNT ; ! "~every kind"
әр:һәр DET-QNT ; ! "every" Dir/LR
бар:бар DET-QNT ; ! "all"
барлық:барлық DET-QNT ; ! "all"
бірқатар:бірқатар DET-QNT ; ! "several"
бірнеше:бірнеше DET-QNT ; ! "some"
бүкіл:бүкіл DET-QNT ; ! "whole"
көптеген:көптеген DET-QNT ; ! "many"
қанша:қанша DET-QNT ; ! "how many"

ондаған:ондаған DET-QNT ; ! ""
жүздеген:жүздеген DET-QNT ; ! ""
мыңдаған:мыңдаған DET-QNT ; ! ""
түмендеген:түмендеген DET-QNT ; ! ""
миллиондаған:миллиондаған DET-QNT ; ! ""
миллиардтаған:миллиардтаған DET-QNT ; ! ""
күллі:күллі DET-QNT ; ! ""
исі:исі DET-QNT ; ! ""

әлдебір:әлдебір DET-IND ; ! "a"
бір:бір DET-IND ; ! "a"
кей:кей DET-IND ; ! "some"
кейбір:кейбір DET-IND ; ! "some"
қайсыбір:қайсыбір DET-IND ; ! "some"
әлденеше:әлденеше DET-IND ; ! ""
бірер:бірер DET-IND ; ! ""

қай:қай DET-ITG ; ! "which"
қайсы:қайсы DET-ITG ; ! "which"

ешбір:ешбір DET-NEG ; ! "none, no"
ешқандай:ешқандай DET-NEG ; ! "no [kind of], none"

өз:өз DET-REF ; ! "oneself's"


!================!
 LEXICON Numerals
!================!


нөл:нөл NUM ; ! "zero"
бір:бір NUM ; ! "one"
екі:екі NUM ; ! "two"
үш:үш NUM ; ! "three"
төрт:төрт NUM ; ! "four"
бес:бес NUM ; ! "five"
алты:алты NUM ; ! "six"
жеті:жеті NUM ; ! "seven"
сегіз:сегіз NUM ; ! "eight"
тоғыз:тоғыз  NUM ; ! "nine"
он:он NUM ; ! "ten"

он% бір:он% бір NUM ; ! "eleven"
он% екі:он% екі NUM ; ! "twelve"
он% үш:он% үш NUM ; ! "thirteen"
он% төрт:он% төрт NUM ; ! "fourteen"
он% бес:он% бес NUM ; ! "fifteen"
он% алты:он% алты NUM ; ! "sixteen"
он% жеті:он% жеті NUM ; ! "seventeen"
он% сегіз:он% сегіз NUM ; ! "eighteen"
он% тоғыз:он% тоғыз NUM ; ! "nineteen"

жиырма:жиырма NUM-TWENTY ; ! "twenty"
отыз:отыз NUM ; ! "thirty"
қырық:қырық NUM ; ! "forty"
елу:елу NUM ; ! "fifty"
алпыс:алпыс NUM ; ! "sixty"
жетпіс:жетпіс NUM ; ! "seventy"
сексен:сексен NUM ; ! "eighty"
тоқсан:тоқсан NUM ; ! "ninety"
жүз:жүз NUM ; ! "hundred"

мың:мың NUM ; ! "thousand"
түмен:түмен NUM ; ! "archaic for 10 000"
миллион:миллион NUM ; ! "million"
миллиард:миллиард NUM ; ! "billion"
триллион:триллион NUM ; ! "trillion"

! Technically this goes on to higher numbers,
! but its use beyond 7 is rare and is of questionable grammaticality
бір:біреу NUM-COLL ;
екі:екеу NUM-COLL ;
үш:үшеу NUM-COLL ;
төрт:төртеу NUM-COLL ;
бес:бесеу NUM-COLL ;
алты:алтау NUM-COLL ;
жеті:жетеу NUM-COLL ;

I NUM-ROMAN ; ! ""
II NUM-ROMAN ; ! ""
III NUM-ROMAN ; ! ""
IV NUM-ROMAN ; ! ""
V NUM-ROMAN ; ! ""
VI NUM-ROMAN ; ! ""
VII NUM-ROMAN ; ! ""
VIII NUM-ROMAN ; ! ""
IX NUM-ROMAN ; ! ""
X NUM-ROMAN ; ! ""
XI NUM-ROMAN ; ! ""
XII NUM-ROMAN ; ! ""
XIII NUM-ROMAN ; ! ""
XIV NUM-ROMAN ; ! ""
XV NUM-ROMAN ; ! ""
XVI NUM-ROMAN ; ! ""
XVII NUM-ROMAN ; ! ""
XVIII NUM-ROMAN ; ! ""
XIX NUM-ROMAN ; ! ""
XX NUM-ROMAN ; ! ""
XXI NUM-ROMAN ; ! ""

неше:неше NUM-ITG ;


!=============!
 LEXICON Nouns

бақша:бақша N1 ; ! "garden house"
банк:банк N5 ; ! "bank"
бастық:бастық N1 ; ! "head,chief"
жас:жас N1 ; ! "age"
кітап:кітап N1 ; ! "book"
қол:қол N1 ; ! "hand"
сағат:сағат N1 ; ! "hour"
сыра:сыра N1 ; ! "beer"
шелек:шелек N1 ; ! "bucket"

 LEXICON ProperNouns

Иерусалим:Иерусалим%{☭%} NP-TOP ; ! "Jerusalem"
Айгүл:Айгүл NP-ANT-F; ! "Aygül" (Kazakh)

 LEXICON Adjectives

дұрыс:дұрыс A1 ; ! "correct"
жақсы:жақсы A1 ; ! "good"
жаман:жаман A1 ; ! "bad"
жаңа:жаңа A1 ; ! "new"
керек:керек A1 ; ! ""

 LEXICON Adverbs

әбден:әбден ADV ; ! "extremely"
қайта:қайта ADV ; ! "again"
өте:өте ADV ; ! "very"
ертең:ертең ADV ; ! ""
қазір:қазір ADV ; ! ""
жаңа:жаңа ADV-WITH-KI ; ! "a while ago's"
қазір:қазір ADV-WITH-KI ; ! "now > the one now"

 LEXICON Verbs

біл:біл V-TV ; ! "know,understand"
бол:бол V-IV ; ! "1. быть"
бол:бол V-IV ; ! "be/become"
іш:іш V-TV ; ! "drink"
кел:кел V-IV ; ! "come"
көр:көр V-TV ; ! "see"
қара:қара V-TV ; ! "look at"
ойна:ойна V-TV ; ! "play"
бол:бол Vinfl-AUX ; !
кел:кел Vinfl-AUX ; !

 LEXICON Interjections
!=====================!


ау:ау INTERJ ; ! ""
ә:ә INTERJ ; ! ""
әй:әй INTERJ ; ! ""
әйе:әйе INTERJ ; ! "yes"
әлбетте:әлбетте INTERJ ; ! "of course"
әрине:әрине INTERJ ; ! "of course"
әттең:әттең INTERJ ; ! ""
бәрекелді:бәрекелді INTERJ ; ! ""
бәсе:бәсе INTERJ ; ! ""
болыпты:болыпты INTERJ ; ! "okay / that's it"
дариға:дариға INTERJ ; ! ""
демек:демек INTERJ ; ! "that is"
дұрыс:дұрыс INTERJ ; ! "right"
ей:ей INTERJ ; ! ""
жарайды:жарайды INTERJ ; ! "okay / it'll do"
жоқ:жоқ INTERJ ; ! "no"
иә:иә INTERJ ; ! "yes"
қане:қане INTERJ ; ! "where" ! interrog. interjection (question form of "вот")
қош:қош INTERJ ; ! ""
Құдай%-ай:Құдай%-ай INTERJ ; ! ""
мақұл:мақұл INTERJ ; ! "fine/okay"
мә:мә INTERJ ; ! "here you go / take"
мейлі:мейлі INTERJ ; ! "okay/fine"
міне:міне INTERJ ; ! "here is, вот"
мына:мына INTERJ ; ! "here is, вот"
ой:ой INTERJ ; ! "hey"
өй:өй INTERJ ; ! ""
түсінікті:түсінікті INTERJ ; ! "understood"
уа:уа INTERJ ; ! ""
әу:әу INTERJ ; ! ""
ойбай:ойбай INTERJ ; ! ""

! Вводные слова
! =============

айтпақшы:айтпақшы INTERJ ; ! ""
бәлкім:бәлкім INTERJ ; ! ""
есесіне:есесіне INTERJ ; ! ""
мәселен:мәселен INTERJ ; ! "e.g."
мысалы:мысалы INTERJ ; ! "e.g."

!==================!
 LEXICON Ideophones
!==================!

арс:арс IDEO ; ! ""
жарқ:жарқ IDEO ; ! ""

!=====================!
 LEXICON Abbreviations
!=====================!


BAE:BAE ABBR ; ! ""
BG:BG ABBR ; ! ""
CEZ:CEZ ABBR ; ! ""
CNPC:CNPC ABBR ; ! ""
ER:ER ABBR ; ! ""
HAS:HAS ABBR ; ! ""
IMD:IMD ABBR ; ! ""
Kaz:Kaz ABBR ; ! ""
KPO:KPO ABBR ; ! ""
KZ:KZ ABBR ; ! ""
kz:kz ABBR ; ! ""
ААҚ:ААҚ ABBR ; ! "Ашық акционердық коғам=Public company"
АК:АК ABBR ; ! ""
АКСР:АКСР ABBR ; ! ""
АҚШ:АҚШ ABBR ; ! "USA"
АӨСШК:АӨСШК ABBR ; ! ""
аум%.:аум%. ABBR ; ! ""
б%.з%.б%.:б%.з%.б%. ABBR ; ! "CE"
б%.з%.б%.:б%.з%.б ABBR ; ! "CE" Dir/LR
б%.з%.д%.:б%.з%.д%. ABBR ; ! "BCE"
б%.з%.д%.:б%.з%.д ABBR ; ! "BCE" Dir/LR
биікт%.:биікт%. ABBR ; ! ""
биол%.:биол%. ABBR ; ! ""
Биохим:Биохим ABBR ; ! "Разведка&Добыча"
БӨ:БӨ ABBR ; ! ""
БТА:БТА ABBR ; ! "a bank in Kazakhstan"
БҰҰ:БҰҰ ABBR ; ! "United Nations"
ВИЧ:ВИЧ ABBR ; ! ""
геогр%.:геогр%. ABBR ; ! ""
геол%.:геол%. ABBR ; ! ""
Гленкор:Гленкор ABBR ; ! ""
ГЭС:ГЭС ABBR ; ! ""
ДНҚ:ДНҚ ABBR ; ! ""
ЕҚЫҰ:ЕҚЫҰ ABBR ; ! "eng. OSCE, rus. ОБСЕ"
Ембімұнайгаз:Ембімұнайгаз ABBR ; ! ""
ЕурАзЭҚ:ЕурАзЭҚ ABBR ; ! ""
Еуроодақ:Еуроодақ ABBR ; ! ""
ж%.:ж%. ABBR ; ! "year"
ж%.:ж ABBR ; ! "year" Dir/LR
жж%.:жж%. ABBR ; ! ""
ЖІӨ:ЖІӨ ABBR ; ! ""
ЖСДП:ЖСДП ABBR ; ! "Жалпыұлттық социал демократиялық партия=Гомуммилли Социаль Демократик Партия"
ЖШС:ЖШС ABBR ; ! "LLC"
ІІМ:ІІМ ABBR ; ! "Ішке істер министрліғі"
КГБ:КГБ ABBR ; ! ""
кг:кг ABBR ; ! "kg"
км²:км² ABBR ; ! "km²"
км²:км2 ABBR ; ! "km²" Dir/LR
км³:км³ ABBR ; ! "km³"
км³:км3 ABBR ; ! "km³" Dir/LR
км:км ABBR ; ! "km"
КСР:КСР ABBR ; ! ""
КСРО:КСРО ABBR ; ! "USSR"
КХДР:КХДР ABBR ; ! ""
Қазатомпром:Қазатомпром ABBR ; ! ""
ҚазКСР:ҚазКСР ABBR ; ! ""
ҚазМУ:ҚазМУ ABBR ; ! "Al-Farabi Kazakh National Universty"
Қазмұнайгаз:Қазмұнайгаз ABBR ; ! ""
Қазпошта:Қазпошта ABBR ; ! ""
ҚазТАГ:ҚазТАГ ABBR ; ! ""
ҚазҰУ:ҚазҰУ ABBR ; ! "Al-Farabi Kazakh National Universty"
ҚКП:ҚКП ABBR ; ! ""
ҚМДБ:ҚМДБ ABBR ; ! "Казакъстан Мөселманнары Диния Нәзараты"
ҚР:ҚР ABBR ; ! "Republic of Kazakhstan"
ҚХР:ҚХР ABBR ; ! ""
лат%.:лат%. ABBR ; ! ""
м²:м² ABBR ; ! "m²"
м²:м2 ABBR ; ! "m²" Dir/LR
м³:м³ ABBR ; ! "m³"
м³:м3 ABBR ; ! "m³" Dir/LR
МАГАТЭ:МАГАТЭ ABBR ; ! ""
млн:млн ABBR ; ! "million"
млрд:млрд ABBR ; ! "billion"
м:м ABBR ; ! "m"
мм:мм ABBR ; ! "mm"
МТК:МТК ABBR ; ! ""
мыс%.:мыс%. ABBR ; ! ""
НАТО:НАТО ABBR ; ! ""
обл%.:обл%. ABBR ; ! ""
ОГПУ:ОГПУ ABBR ; ! ""
оңт%.:оңт%. ABBR ; ! "оңтүстік,көньяк,south" FIXME:CHECK -->"
Өзенмұнайгаз:Өзенмұнайгаз ABBR ; ! ""
ӨИҚ:ӨИҚ ABBR ; ! "Үзбәкстан Ислам Хәрәкәте"
ӨФ:ӨФ ABBR ; ! ""
ПӘК:ПӘК ABBR ; ! ""
пед%.:пед%. ABBR ; ! ""
РКФСР:РКФСР ABBR ; ! ""
РНҚ:РНҚ ABBR ; ! ""
РФ:РФ ABBR ; ! "Russian Federation"
см:см ABBR ; ! "cm"
СНПС:СНПС ABBR ; ! ""
солт%.:солт%. ABBR ; ! "солтүстік,төньяк,north" FIXME:CHECK -->"
солт%.:солт ABBR ; ! "солтүстік,төньяк,north" FIXME:CHECK -->" Dir/LR
СПИД:СПИД ABBR ; ! ""
ССРО:ССРО ABBR ; ! ""
СССР:СССР ABBR ; ! "USSR"
ССС:ССС ABBR ; ! ""
СЭС:СЭС ABBR ; ! ""
т%.б%.:т%.б%. ABBR ; ! "etc."
тереңд%.:тереңд%. ABBR ; ! ""
тех%.:тех%. ABBR ; ! ""
ТМД:ТМД ABBR ; ! "CIS"
ТР:ТР ABBR ; ! "Republic of Tatarstan"
т%.:т%. ABBR ; ! "tonne"
УЕФА:УЕФА ABBR ; ! ""
ҰҚК:ҰҚК ABBR ; ! "Ұжымдық қауіпсіздік келісімі"
ФСБ:ФСБ ABBR ; ! ""
хим%.:хим%. ABBR ; ! "chemical"
ШЫҰ:ШЫҰ ABBR ; ! "Shanghai Cooperaton Organization"
экон%.:экон%. ABBR ; ! "economic"
ЮНЕСКО:ЮНЕСКО ABBR ; ! ""
EITI:EITI ABBR ; ! ""
ЕО:ЕО ABBR ; ! ""

!===================!
 LEXICON Punctuation
!===================!


%.%<sent%>:%. # ;
%.%.%<sent%>:%.%. # ;
%.%.%.%<sent%>:%.%.%. # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
%\%<sent%>:%\ # ;
%/%<sent%>:%/ # ;


!==============!
 LEXICON Digits ! Use/Circ
!==============!


%0:%0 NUM ;  ! Use/Circ
№:№ DIGITLEX ;  ! Use/Circ
DIGITLEX ; ! Use/Circ
LASTDIGIT ; ! Use/Circ
LASTDIGIT-REST ; ! Use/Circ

!LEXICON Guesser
!
!<(а | ә | б | в | г | ғ | д | е | ё | ж | з | и | і | й | к | қ | л | м | н | ң | о | ө | п | р | с | т | у | ұ | ү | ф | х | һ | ц | ч | ш | щ | ь | ы | ъ | э | ю | я)+> N1 ; 
